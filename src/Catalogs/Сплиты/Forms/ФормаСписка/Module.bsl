//@skip-check module-region-empty
#Область ОписаниеПеременных
#КонецОбласти

//@skip-check module-region-empty
#Область ОбработчикиСобытийФормы
#КонецОбласти

//@skip-check module-region-empty
#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗагрузитьС_MOEX(Команда)
	СтруктураФоновогоЗадания = ВыполнитьФоновоеЗаданиеНаСервере( , УникальныйИдентификатор);
	
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал  = 1;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(СтруктураФоновогоЗадания, 
												Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект), 
												ПараметрыОжидания);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ВыполнитьФоновоеЗаданиеНаСервере(ПараметрыЗапуска, УникальныйИдентификатор)
	НаименованиеЗадания = "Загрузка справочника Сплиты";
	ВыполняемыйМетод = "Справочники.Сплиты.ЗагрузитьС_MOEXНаСервере";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.ЗапуститьВФоне 	= Истина;
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор); 
	
	СтруктураФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыЗапуска, ПараметрыВыполнения);
	Возврат СтруктураФоновогоЗадания;
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт
	//@skip-check bsl-legacy-check-string-literal
	ОтключитьОбработчикОжидания("ОбработчикОжиданияИндикатор");
	Элементы.Список.Обновить();
	
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(ПолучитьИзВременногоХранилища(Результат.АдресРезультата));
		УдалитьИзВременногоХранилища(Результат.АдресРезультата); 
	КонецЕсли;
КонецПроцедуры
#КонецОбласти
