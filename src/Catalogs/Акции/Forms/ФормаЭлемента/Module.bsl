
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.SECID.ТолькоПросмотр = НЕ Объект.Ссылка.Пустая(); 
	СсылкаMOEX = БондОбщий.СсылкаНаТикер(Объект.SECID);
	ОбновитьВидимостьЭлементов();
КонецПроцедуры

#КонецОбласти


//@skip-check module-region-empty
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаMOEXНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(СсылкаMOEX);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьС_MOEX(Команда)
	ЗагрузитьС_MOEXНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПереименования(Команда)
	ЗагрузитьПереименованияНаСервере(Объект.SECID);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьПереименованияНаСервере(secid)
	Результат = Справочники.Переименования.СписокПереименований(secid);
	Сообщить(Результат);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьС_MOEXНаСервере()
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	Переименования = Справочники.Переименования.СписокПереименований(ВРег(СправочникОбъект.SECID));
	БондОбщий.ЗагрузитьС_MOEX(Переименования[Переименования.Количество()-1].Код, СправочникОбъект);
	СправочникОбъект.ИзменениеТорговыхКодов.Очистить();
	Для Каждого Элемент Из Переименования Цикл
		ЭлементТЧ = СправочникОбъект.ИзменениеТорговыхКодов.Добавить();
		ЗаполнитьЗначенияСвойств(ЭлементТЧ, Элемент);		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	СсылкаMOEX = БондОбщий.СсылкаНаТикер(СправочникОбъект.SECID);
	ОбновитьВидимостьЭлементов();
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьЭлементов()
	Если Объект.GROUP = "stock_etf" Тогда
		Элементы.FACEUNIT.Доступность = Ложь;
		Элементы.FACEVALUE.Доступность = Ложь;
		Элементы.ISSUESIZE.Доступность = Ложь;
	Иначе
		Элементы.FACEUNIT.Доступность = Истина;
		Элементы.FACEVALUE.Доступность = Истина;
		Элементы.ISSUESIZE.Доступность = Истина;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти