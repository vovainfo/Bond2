
// SPDX-License-Identifier: Apache-2.0+

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Сертификат = Параметры.Сертификат;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Асинх Процедура Объединить(Команда)
	Если ПустаяСтрока(Сертификат) Тогда
		СообщитьПользователю("Укажите путь к файлу сертификата", "Сертификат");
		Возврат;
	КонецЕсли;
	Если ПустаяСтрока(Ключ) Тогда
		СообщитьПользователю("Укажите путь к файлу ключа сертификата", "Ключ");
		Возврат;
	КонецЕсли;
	Если ПустаяСтрока(Результат) Тогда
		СообщитьПользователю("Укажите путь к файлу результата конкатенации", "Результат");
		Возврат;
	КонецЕсли;
	
	РезультатОперации = Ждать КлиентHTTPСлужебный.ОбъединенныйФайлСертификатаИКлючаPEM(
		Результат,
		Новый Файл(Сертификат),
		Новый Файл(Ключ),
		ЗапретитьПерезапись
	);
	Если РезультатОперации.Ошибка Тогда
		СообщитьПользователю(РезультатОперации.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	Закрыть(Результат);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы
&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ВыборФайлаСертификатаЗавершение", ЭтотОбъект);
		
	ПоказатьДиалогВыбораФайла(Оповещение, "Выбор файла сертификата", "PEM (*.crt)|*.crt|PEM (*.pem)|*.pem|PEM (*.cer)|*.cer|Все файлы|*.*");
КонецПроцедуры

&НаКлиенте
Процедура КлючНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ВыборФайлаКлючаСертификатаЗавершение", ЭтотОбъект);
		
	ПоказатьДиалогВыбораФайла(Оповещение, "Выбор файла ключа сертификата", "PEM (*.key)|*.key|PEM (*.pem)|*.pem|Все файлы|*.*");
КонецПроцедуры

&НаКлиенте
Процедура РезультатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ВыборФайлаРезультатаЗавершение", ЭтотОбъект);
		
	ПоказатьДиалогСохраненияФайла(Оповещение, "Выбор файла для создания", "PEM (*.crt)|*.crt|PEM (*.pem)|*.pem|PEM (*.cer)|*.cer|Все файлы|*.*");
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СообщитьПользователю(Знач Текст, Знач Поле = Неопределено)
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	
	Если Поле <> Неопределено Тогда
		Сообщение.Поле  = Поле;
	КонецЕсли;
	
	Сообщение.Сообщить();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогВыбораФайла(Знач Оповещение, Знач Заголовок = "Выбор файла", Знач Фильтр = Неопределено)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок                   = Заголовок;
	Диалог.ПредварительныйПросмотр     = Ложь;
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Диалог.МножественныйВыбор          = Ложь;
	
	Если Фильтр <> Неопределено Тогда
		Диалог.Фильтр = Фильтр;
	КонецЕсли;
	
	Диалог.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогСохраненияФайла(Знач Оповещение, Знач Заголовок = "Выбор файла", Знач Фильтр = Неопределено)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок                   = Заголовок;
	Диалог.ПредварительныйПросмотр     = Ложь;
	Диалог.ПроверятьСуществованиеФайла = Ложь;
	Диалог.МножественныйВыбор          = Ложь;
	
	Если Фильтр <> Неопределено Тогда
		Диалог.Фильтр = Фильтр;
	КонецЕсли;
	
	Диалог.Показать(Оповещение);
КонецПроцедуры

#Область ОБРАТНЫЕ_ВЫЗОВЫ
&НаКлиенте
Процедура ВыборФайлаСертификатаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы <> Неопределено Тогда
		Сертификат = ВыбранныеФайлы[0];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаКлючаСертификатаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы <> Неопределено Тогда
		Ключ = ВыбранныеФайлы[0];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаРезультатаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы <> Неопределено Тогда
		Результат = ВыбранныеФайлы[0];
	КонецЕсли;
КонецПроцедуры
#КонецОбласти
#КонецОбласти
